// ./gradlew build
// ./gradlew bintrayUpload

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/niqdev/ipcam-view'
def gitUrl = 'https://github.com/niqdev/ipcam-view.git'
def libraryName = 'mjpeg-view'

install {
    repositories.mavenInstaller {
        // generates POM.xml with proper parameters
        pom {
            project {
                name libraryName
                description 'Android Mjpeg video streaming'
                url siteUrl

                packaging 'aar'
                groupId project.group
                artifactId libraryName
                version project.version

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/niqdev/ipcam-view/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'niqdev'
                        name 'niqdev'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    // to use it, you need environment variables. With this, everyone can build it out of the box
    user = System.getenv("bintray.user")
    key = System.getenv("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libraryName
        version {
            name = project.version
            released = new Date()
        }
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
    }
}
